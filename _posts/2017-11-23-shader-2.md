---
layout: post
author: skyousuke
title: "Shader ใน LibGDX เบื้องต้น ตอนที่ 2"
category: "shader"
image: https://telluric-chimneys.000webhostapp.com/images/shader-6.png
lang: th_TH
---

ต่อจากตอนที่แล้ว เราลองเขียนโปรแกรม shader ทีเปลี่ยนสี pixel ทั้งหมดเป็นสีแดงแล้ว ในตอนนี้เราจะลองมาเขียนโปรแกรม shader ที่เป็น default shader กัน


## ลองสร้าง Default shader

default shader คือ shader ที่ไม่ได้ตกแต่งภาพอะไรเพิ่มเติมเลย ก่อนอื่นเริ่มจากเขียน vertex shader ก่อน โดยเขียนเหมือนกับอันที่แล้ว

`shader-default.vert`

```c
uniform mat4 u_projTrans;

attribute vec4 a_position;

void main() {
    gl_Position = u_projTrans * a_position;
}
```

ทีนี้เราต้องส่งข้อมูล texture ไปให้กับ fragment shader ด้วย ซึ่งข้อมูล texture จะส่งมาในรูปของระบบพิกัด texture โดยที่
ระบบพิกัดของ texture จะใช้ระบบแบบ 2 มิติ คือระบบ ST มีค่าตั้งแต่ (0, 0) ไปถึง (1, 1) ตามรูป 

![](https://telluric-chimneys.000webhostapp.com/images/shader-6.png)

ดังนั้นตัวแปรจะมีชนิด `vec2` และ libGDX framework จะส่งค่ามาให้กับตัวแปรที่ชื่อ `a_texCoord0` ดังนั้น จะเขียน code ได้ดังนี้


`shader-default.vert`

```c
uniform mat4 u_projTrans;

attribute vec4 a_position;
attribute vec2 a_texCoord0;

void main() {
    gl_Position = u_projTrans * a_position;
}
```
 
สังเกตว่า `a_texCoord0` เป็นตัวแปรแบบ `attribute` ด้วย เพราะเป็นข้อมูลที่อยู่ใน `buffer object`

จากนั้นเราจะส่ง `a_texCoord0` จาก vertex shader ไปให้กับ fragment shader เพื่อใช้แปลงระบบพิกัด texture เป็นค่าสีของแต่ละ fragment โดยส่งผ่านตัวแปรแบบ `varying` ดังนั้นให้เราสร้างตัวแปร `varying` ชนิด `vec2` และให้มีค่าเท่ากับ `a_texCoord0` ดังนี้

`shader-default.vert`

```c
uniform mat4 u_projTrans;

attribute vec4 a_position;
attribute vec2 a_texCoord0;

varying vec2 juju_texCoord;

void main() {
    gl_Position = u_projTrans * a_position;
    juju_texCoord = a_texCoord0;
}
```

จะตั้งชื่อว่าอะไรก็ได้นะ ส่วนใหญ่นิยมใช้ชื่อ `v_texCoord` กัน แต่ในที่นี้ จะแสดงให้เห็นว่าใช้ชื่ออะไรก็ได้ จึงตั้งชื่อว่า `juju_texCoord` อิอิ

ทาง vertex shader ก็มีเพียงเท่านี้ ก็คืออ่านค่า `a_texCoord0` ส่งให้กับ fragment shader ผ่านตัวแปรที่ชื่อ `juju_texCoord` ทีนี้เรามาดู fragment shader กัน

สำหรับ fragment shader เราก็เขียนเริ่มต้นเหมือนกับตอนที่แล้ว copy มาเลย

`shader-default.frag`

```c
void main() {
    gl_FragColor = vec4(1.0, 0.0, 0.0, 1.0);
}
```

เราก็ต้องสร้างตัวแปร `varying` ชื่อ `juju_texCoord` เพื่อรับค่าที่ส่งมาจากตัว vertex shader ดังนั้นจะได้ code เป็นแบบนี้

`shader-default.frag`

```c
varying vec2 juju_texCoord;

void main() {
    gl_FragColor = vec4(1.0, 0.0, 0.0, 1.0);
}
```

นอกจากตัว `juju_texCoord` (พิกัด texture) ที่เรารับค่ามาแล้ว ยังจะต้องมีข้อมูลของตัว texture เองด้วย ซึ่งเราต้องรับค่ามาจากตัว libGDX ด้วยผ่านตัวแปรแบบ `uniform` ชนิด `sampler2D` ที่ชื่อ `u_texture` ดังนั้นจะได้ code เป็น

`shader-default.frag`

```c
uniform sampler2D u_texture;

varying vec2 juju_texCoord;

void main() {
    gl_FragColor = vec4(1.0, 0.0, 0.0, 1.0);
}
```

ตอนนี้เราก็ได้ข้อมูลทั้งระบบพิกัด texture และตัวข้อมูล texture มาแล้ว เราสามารถหาค่าสีของ texture แต่ละ pixel (หรือ fragment) ได้แล้ว โดยใช้ built-in function ตัวนี้

`shader-default.frag`

```c
vec4 texture2D(sampler2D sampler, vec2 coord)  
```

ฟังก์ชันนี้ใช้หาค่าสีของ texture จากค่าพิกัดที่กำหนด ดังนั้นเราจะเขียน code ได้เป็น

`shader-default.frag`

```c
uniform sampler2D u_texture;

varying vec2 juju_texCoord;

void main() {
    vec4 juju_texColor = texture2D(u_texture, juju_texCoord);
    gl_FragColor = vec4(1.0, 0.0, 0.0, 1.0);
}
```

ปล. จะตั้งชื่อว่าอะไรก็ได้นะ แต่ต้องเป็น `vec4` นะจะ ในที่นี้ใช้ `juju_texColor` ไปขำๆ 555
{: .notice}

ตอนนี้เราก็ได้ค่าสีของ texture ละ ซึ่งก็เป็นค่าสีของแต่ละ pixel นั่นแหละ และเราก็ต้องการเขียน shader ที่ไม่ได้ตกแต่งสีอะไรเลย ดังนั้นเอาค่าสีของ texture ไปใส่ใน `gl_FragColor` ได้เลย

`shader-default.frag`

```c
uniform sampler2D u_texture;
varying vec2 juju_texCoord;

void main() {
    vec4 juju_texColor = texture2D(u_texture, juju_texCoord);
    gl_FragColor = juju_texColor;
}
```

หรือเขียนให้กระชับหน่อย ก็เป็น

`shader-default.frag`

```c
uniform sampler2D u_texture;
varying vec2 juju_texCoord;

void main() {
    gl_FragColor = texture2D(u_texture, juju_texCoord);
}
```

เพียงแค่เราได้ได้ default shader (shader ที่ไม่ได้ตกแต่งสีอะไรเลย) แล้ว เราลองไป run กันดูเลย โดยใช้ shader ล่าสุดอันที่เราเขียนเมื่อกี้

```java
import com.badlogic.gdx.ApplicationAdapter;
import com.badlogic.gdx.Gdx;
import com.badlogic.gdx.graphics.GL20;
import com.badlogic.gdx.graphics.Texture;
import com.badlogic.gdx.graphics.g2d.SpriteBatch;
import com.badlogic.gdx.graphics.glutils.ShaderProgram;

public class ShaderTutorial extends ApplicationAdapter {

    private Texture image;
    private SpriteBatch batch;
    private ShaderProgram shader;

    @Override
    public void create() {
        image = new Texture("yourname.png");

        batch = new SpriteBatch();
        shader = new ShaderProgram(
                Gdx.files.internal("shader-default.vert"),
                Gdx.files.internal("shader-default.frag"));
        batch.setShader(shader);
        ShaderProgram.pedantic = false;
    }

    @Override
    public void render() {
        Gdx.gl.glClearColor(0.2f, 0.2f, 0.2f, 1);
        Gdx.gl.glClear(GL20.GL_COLOR_BUFFER_BIT);

        batch.begin();
        batch.draw(image, 0, 0);
        batch.end();
    }

    @Override
    public void dispose() {
        batch.dispose();
        shader.dispose();
        image.dispose();
    }
}
```

จะพบว่าภาพที่แสดงก็จะเหมือนกับปกติ (เหมือนไม่ได้ใส่ shader) เพราะว่า shader เราไม่ได้มีการแต่งสีใดๆนั่นเอง

![](https://telluric-chimneys.000webhostapp.com/images/shader-7.png)

ต่อไปเราลองเขียน shader แบบอื่นๆกันบ้าง

## ลองสร้าง Shader ที่ปรับค่าน้ำหนักของ RGBA Channel ทั้ง 4

Shader นี้จะเริ่มมีการยุ่งเกี่ยวกับค่าสีของ texture กันแล้ว โดยเราจะลองปรับค่าน้ำหนักของ RBGA Channel กันดูว่า จะได้ผลลัพธ์ออกมาเป็นอย่างไร เริ่มจาก vertex shader ตั้งชื่อไฟล์เป็น `shader-color.vert` และใช้ code เหมือนกับ default shader ที่เราเขียนเมื่อกี้

`shader-color.vert`

```c
uniform mat4 u_projTrans;

attribute vec4 a_position;
attribute vec2 a_texCoord0;

varying vec2 juju_texCoord;

void main() {
    gl_Position = u_projTrans * a_position;
    juju_texCoord = a_texCoord0;
}
```

ส่วน fragment shader  ตั้งชื่อไฟล์เป็น `shader-color.frag` แล้วก็ copy จาก default shader มาเหมือนกัน

`shader-color.frag`

```c
uniform sampler2D u_texture;
varying vec2 juju_texCoord;

void main() {
    gl_FragColor = texture2D(u_texture, juju_texCoord);
}
```

ทีนี้เราจะทำการปรับค่าน้ำหนักของ RBGA Channel ดังนั้นให้เอาค่าสีของ texture ไปเก็บในตัวแปร `texColor` ก่อน

`shader-color.frag`

```c
uniform sampler2D u_texture;
varying vec2 juju_texCoord;

void main() {
    vec4 texColor = texture2D(u_texture, juju_texCoord);
    gl_FragColor = texColor;
}
```

จากนั้นนำ `texColor` โดยการนำไปคูณกับค่าน้ำหนักที่เราต้องการ (`texColor` เป็น `vec4` ดังนั้นค่าที่นำมาคูณจึงต้องเป็น `vec4` ด้วย) อย่างเช่นตาม code ด้านล่างนี้เป็นการคูณค่าน้ำหนักของทุก channel เท่ากับ 1.0 (เป็นค่าของ channel RGBA ทั้งสี่ เรียงกันตามลำดับ) 

`shader-color.frag`

```c
uniform sampler2D u_texture;
varying vec2 juju_texCoord;

void main() {
    vec4 texColor = texture2D(u_texture, juju_texCoord);
    gl_FragColor = texColor * vec4(1.0, 1.0, 1.0, 1.0);
} 
```

ถ้าลอง run ดูจะพบว่าไม่มีผลอะไร เพราะเราไม่ได้เปลี่ยนค่าน้ำหนักของมัน ทีนี้ลองลดค่าน้ำหนักของของ channel สีเขียว (G) กับสีน้ำเงิน (B) ลงเหลือครึ่งเดียวแบบนี้

`shader-color.frag`

```c
uniform sampler2D u_texture;
varying vec2 juju_texCoord;

void main() {
    vec4 texColor = texture2D(u_texture, juju_texCoord);
    gl_FragColor = texColor * vec4(1.0, 0.5, 0.5, 1.0);
} 
```

แล้วลอง run ดูจะพบว่า ภาพมีสีแดงขึ้น เพราะส่วนที่เป็นสีเขียวและสีน้ำเงิน ถูกลดค่าน้ำหนักลงไปนั่นเอง

![](https://telluric-chimneys.000webhostapp.com/images/shader-8.png)

ทีนี้ลองปรับน้ำหนักเล่นดู แล้วลองสังเกตผลลัพธ์ที่ได้ดูกันนะจ๊ะ

`shader-color.frag`

```c
uniform sampler2D u_texture;
varying vec2 juju_texCoord;

void main() {
    vec4 texColor = texture2D(u_texture, juju_texCoord);
    gl_FragColor = texColor * vec4(0.5, 1.0, 0.5, 1.0);
} 
```

![](https://telluric-chimneys.000webhostapp.com/images/shader-9.png)

`shader-color.frag`

```c
uniform sampler2D u_texture;
varying vec2 juju_texCoord;

void main() {
    vec4 texColor = texture2D(u_texture, juju_texCoord);
    gl_FragColor = texColor * vec4(0.5, 0.5, 1.0, 1.0);
} 
```

![](https://telluric-chimneys.000webhostapp.com/images/shader-10.png)

เพียงเท่านี้เราก็จะได้ shader ปรับค่าสีแบบง่ายๆแล้ว 

## ลองสร้าง Shader ที่แสดงผลแค่ Channel เดียว

Shader อันที่แล้วเป็นการปรับค่าน้ำหนัก แต่อันนี้เราจะไม่ปรับละ แต่เราจะเอาค่าน้ำหนักเดิมของภาพ มาแสดงผลในรูปแบบแยก Channel กันเลย

**ข้อมูลน่ารู้:** Channel คือสิ่งที่ใช้แสดงถึงปริมาณของสีใน Channel นั้นๆด้วยสีโทนขาวดำ ยิ่งเป็นสีขาวแสดงว่ามีสีใน Channel นั้นมาก ถ้าเป็นสีดำแปลว่าไม่มีสีใน Channel นั้นเลย ดังนั้น Channel ของสีไม่ใช่เป็นภาพสีนะ อย่าสับสน!
{: .notice--info}

สำหรับ vertex shader ตั้งชื่อว่า `shader-channel.vert` และเราจะเขียนเหมือนเดิม 

`shader-channel.vert`

```c
uniform mat4 u_projTrans;

attribute vec4 a_position;
attribute vec2 a_texCoord0;

varying vec2 juju_texCoord;

void main() {
    gl_Position = u_projTrans * a_position;
    juju_texCoord = a_texCoord0;
}
```

จากนั้นมาเขียนตัว fragment shader ให้ตั้งชื่อว่า `shader-channel.frag` จากนั้นก็เริ่มเหมือนเดิมก็คือ ดึงค่าสีของ texture มาโดยใช้ฟังก์ชัน `texture2D()`

`shader-channel.frag`

```c
uniform sampler2D u_texture;

varying vec2 juju_texCoord;

void main() {
    vec4 texColor = texture2D(u_texture, juju_texCoord);
} 
```

ทีนี้เราจะลองให้แสดง channel ของสีแดง (red channel) ก่อน โดยการนำค่าน้ำหนักสีแดงของ texture มาเก็บในตัวแปรก่อน
โดยอ่านค่าจากค่าทศนิยมอันแรกที่อยู่ใน `texColor` 

`shader-channel.frag`

```c
uniform sampler2D u_texture;

varying vec2 juju_texCoord;

void main() {
    vec4 texColor = texture2D(u_texture, juju_texCoord);
    float red = texColor.r;
} 
```

**ข้อมูลน่ารู้:** สังเกตว่าเราใช้ `r` เรียกดูค่าทศนิยมตัวแรกใน `vec4` ไม่ได้ใช้ `x` แต่จริงๆแล้วใช้อันไหนก็ได้ เพราะใน glsl เราสามารถเข้าถึงค่าตัวแปรใน `vec4` โดยใช้ `xyxw` หรือ `rgba` หรือ `stqp` ก็ได้ทั้งนั้น ไม่ต่างกัน แต่ไม่ควรใช้ผสมกันมั่วนะ!
{: .notice--info}

ตัวแปร `red` ในตอนนี้เก็บค่าสีแดงไว้แล้ว และเราจะเอาค่านี้มาแสดงในรูปแบบ channel (channel หมายถึงภาพโทนขาวดำ ที่ใช้แสดงปริมาณสี คือส่วนไหนขาวแสดงว่ามีสีแดงมาก ถ้าส่วนไหนดำแสดงว่ามีสีแดงน้อย) ดังนั้นให้แทนค่า RGB ของ pixel ด้วยตัวแปร `red` ได้เลย

`shader-channel.frag`

```c
uniform sampler2D u_texture;

varying vec2 juju_texCoord;

void main() {
    vec4 texColor = texture2D(u_texture, juju_texCoord);
    float red = texColor.r;
    gl_FragColor = vec4(red, red, red, 1.0);
} 
```

ข้อสังเกตก็คือ ถ้า `red` มีค่ามาก `gl_FragColor`(สีของ pixel) ก็จะยิ่งเข้าใกล้สีขาว แต่ถ้ามีค่าน้อยสีก็จะเข้าใกล้สีดำ ซึ่งเป็นลักษณะของ color channel นั่นเอง

เมื่อลอง run ก็จะได้ผลแบบนี้

![](https://telluric-chimneys.000webhostapp.com/images/shader-11.png)

ถ้าอยากใช้ดู green channel หรือ blue channel ก็เปลี่ยนจากอ่านค่า r เป็น g หรือ b แล้วเอาไปใช้สร้างสีแทน แบบนี้

`shader-channel.frag`

```c
uniform sampler2D u_texture;

varying vec2 juju_texCoord;

void main() {
    vec4 texColor = texture2D(u_texture, juju_texCoord);
    float green = texColor.g;
    gl_FragColor = vec4(green, green, green, 1.0);
} 
```

![](https://telluric-chimneys.000webhostapp.com/images/shader-12.png)

`shader-channel.frag`

```c
uniform sampler2D u_texture;

varying vec2 juju_texCoord;

void main() {
    vec4 texColor = texture2D(u_texture, juju_texCoord);
    float blue = texColor.b;
    gl_FragColor = vec4(blue, blue, blue, 1.0);
} 
```

![](https://telluric-chimneys.000webhostapp.com/images/shader-13.png)

อ่านจจะดูว่าคล้ายกัน แต่ลองสังเกตที่โบว์ของชุดนักเรียนที่มิซึฮะใส่ดู 

![](https://telluric-chimneys.000webhostapp.com/images/shader-14.png)

จะเห็นว่าใน red channel โบว์จะมีสีสว่างกว่า เพราะโบว์มีปริมาณสีแดงเยอะกว่า หรือถ้าดูลายเนคไทใน green channel จะมีสีสว่างกว่าเพราะปริมาณสีเขียวเยอะกว่า

## ลองสร้าง Shader ที่แสดงสีแบบ gradient บน texture

เราจะเริ่มเข้าสู่โลกของ shader กันมากขึ้น โดยในครั้งนี้เราจะเรียนรู้เกี่ยวกับ texture coordinate หรือระบบพิกัด texture กันผ่านการสร้างสีแบบ gradient

สีแบบ gradient ก็คือสีที่ไล่ระดับความเข็มไปเรื่อยๆแบบนี้

![](https://telluric-chimneys.000webhostapp.com/images/shader-15.png)

สังเกตว่าในการไล่สี เราต้องรู้พิกัดของ texture ด้วยถึงจะทำได้ เพราะถ้าเราไม่รู้ตำแหน่ง เราก็ไม่สามารถกำหนดความเข้มให้ไล่กันได้หรอก งั้นมาเริ่มเขียนกันเลยดีกว่า

สร้าง vertex shader ตั้งชื่อว่า `shader-gradient.vert` และเราจะเขียนเหมือนเดิมกับอันที่แล้วเลย

`shader-gradient.vert`

```c
uniform mat4 u_projTrans;

attribute vec4 a_position;
attribute vec2 a_texCoord0;

varying vec2 juju_texCoord;

void main() {
    gl_Position = u_projTrans * a_position;
    juju_texCoord = a_texCoord0;
}
```

ทีนี้มาดู fragment shader ดีกว่า ให้ตั้งชื่อว่า `shader-gradient.frag` และเริ่มจากโปรแกรมเปล่าๆ มีเพียงแค่ตัวแปร `juju_texCoord` ที่เรารับค่ามาจาก vertex shader เท่านั้น

`shader-gradient.frag`

```c
varying vec2 juju_texCoord;

void main() {

} 
```

กลับมาดูรูประบบพิกัดของ texture

![](https://telluric-chimneys.000webhostapp.com/images/shader-6.png)

จะเห็นว่า **แนวนอนเป็นแกน s** ส่วน**แนวตั้งเป็นแกน t** โดยทั้ง 2 แกน**มีค่าระหว่าง 0 ถึง 1** (ดูที่ค่า texCoord นะ) โดยที่มุมซ้ายบน จะเป็นพิกัด (0, 0) และมุมขวาล่างเป็นพิกัด (1, 1) นั่นคือ **ยิ่งเลื่อนไปทางขวาค่า s จะมากขึ้น** และ**ยิ่งเลื่อนลงล่างค่า t จะมากขึ้น** (ลองสังเกตดูตามรูปเลยครับ)

ดังนั้น ถ้าเราเอาค่า `s` จาก `juju_texCoord` แบบนี้

`shader-gradient.frag`

```c
varying vec2 juju_texCoord;

void main() {
    float value = juju_texCoord.s;
} 
```

ตัวแปร `value` ใน code ก็จะต้องมีค่าเพิ่มขึ้นเมื่อ pixel อยู่ทางขวามากขึ้น เราลอง**พิสูจน์กันดู**โดยการเขียนแบบนี้

`shader-gradient.frag`

```c
varying vec2 juju_texCoord;

void main() {
    float value = juju_texCoord.s;
    gl_FragColor = vec4(value, value, value, 1.0);
} 
```

ถ้า `value` มีค่ามากขึ้นจากซ้ายไปขวา สีของ pixel ก็จะต้องขาวขึ้นเรื่อยๆจากซ้ายไปขวาด้วยเช่นกัน มาดูกันเลยว่าเป็นจริงหรือเปล่า โดยลอง run โปรแกรมดู

พบว่าได้พบลัพธ์เป็นไปตามที่คิดไว้ คือภาพ fanart ของเรากล้ายเป็น gradient ดำไปขาวตามรูปนี้

![](https://telluric-chimneys.000webhostapp.com/images/shader-16.png)

ดังนั้นแสดงว่าค่า `juju_texCoord.s` มีค่าในลักษณะนี้

![](https://telluric-chimneys.000webhostapp.com/images/shader-17.png)

ต่อมาเราลองมาพิสูจน์ค่า t กันบ้างโดยเขียนคล้ายๆกัน แต่เปลี่ยน `juju_texCoord.s` เป็น `juju_texCoord.t`

`shader-gradient.frag`

```c
varying vec2 juju_texCoord;

void main() {
    float value = juju_texCoord.t;
    gl_FragColor = vec4(value, value, value, 1.0);
} 
```

ก็ได้ผลลัพธ์ตามที่เราคาดไว้ก็คือ ค่า `juju_texCoord.t` ยิ่งลงล่างยิ่งมีค่ามากขึ้น

![](https://telluric-chimneys.000webhostapp.com/images/shader-18.png)

![](https://telluric-chimneys.000webhostapp.com/images/shader-19.png)

ในตอนนี้เราก็รู้แล้วระบบพิกัด texture (`juju_texCoord.s` กับ `juju_texCoord.t`) มีค่าเป็นอย่างไร ดังนั้น เราสามารถประยุกต์ใช้งานในด้านอื่นๆเพิ่มได้เลย งั้นลองเริ่มจากง่ายๆก่อน เอาเป็นเปลี่ยน gradient จากสีดำ-ขาว เป็นสีแดง-ขาวดูดีไหม? 

`shader-gradient.frag`

```c
varying vec2 juju_texCoord;

void main() {
    float value = juju_texCoord.s;
    gl_FragColor = vec4(1.0, value, value, 1.0);
}
```

ผลลัพธ์ที่ได้

![](https://telluric-chimneys.000webhostapp.com/images/shader-20.png)

สังเกตว่าถ้าอยากได้ gradient สีแดง-ขาว ต้องให้ค่าสีแดงเป็น 1.0 ตลอด แต่ให้ค่าสีอื่นค่อยเพิ่มขึ้นแทน ซึ่งมันดูงงๆ เพราะไม่เห็นมีสีขาวเลย และไม่ค่อยสะดวกไปหน่อยนะ ดังนั้นลองมาใช้ `built-in function` อีกตัวนึงของ glsl ช่วยกันเถอะ ครั้งนี้จะแนะนำ function **mix**

```c
vec4 mix(vec4 x, vec4 y, float a)
```

**mix** เป็น function ที่ใช้ผสมค่าระหว่าง 2 ค่าเข้าด้วยกันแบบ linear (สูตรคือ `x * (1-a) + y *a` ) และนี่เป็นตัวช่วยของเรา โดยเราจะเขียน code ใหม่เป็นแบบนี้

`shader-gradient.frag`

```c
varying vec2 juju_texCoord;

void main() {
    float value = juju_texCoord.s;
    const vec4 red = vec4(1.0, 0, 0, 1.0);
    const vec4 white = vec4(1.0, 1.0, 1.0, 1.0);
    gl_FragColor = mix(red, white, value);
}
```

**ข้อสังเกต**: มีการใช้ keyword `const` เข้ามาใน code นี้ด้วย หมายถึงเป็น `ค่าคงที่` นั่นแหละ
{: .notice}

จะได้ผลลัพธ์เหมือนเดิม แต่ code อ่านเข้าใจง่ายขึ้นว่าเป็นสีอะไรผสมสีอะไร มองปุ๊บแล้วบอกเลยได้เลยว่าเป็นสี gradient แดง-ขาว และถ้าเราลองคำนวณค่าดูจะพบว่าเหมือนกันกับ code ที่แล้วเลย

สมมุติให้ s มีค่าเท่ากับ 0.75 ถ้าเป็น code แบบเก่า จะได้ค่าสีเป็น

```c
gl_FragColor = vec4(1.0, 0.75, 0.75, 1.0);
```

ถ้าเป็น code แบบใหม่ จะได้ค่าสีเป็น

```c
mix(red, white, value) = mix(vec4(1.0, 0, 0, 1.0), vec4(1.0, 1.0, 1.0, 1.0), 0.75)

คำนวนได้จากสูตร x * (1 - a) + y * a
r: 1.0 * (1 - 0.75) + 1.0 * 0.75 = 1.0
g: 0.0 * (1 - 0.75) + 1.0 * 0.75 = 0.75
b: 0.0 * (1 - 0.75) + 1.0 * 0.75 = 0.75
a: 1.0 * (1 - 0.75) + 1.0 * 0.75 = 1.0

ทำให้ mix(red, white, value) = vec4(1.0, 0.75, 0.75, 1.0);
ดังนั้น gl_FragColor = vec4(1.0, 0.75, 0.75, 1.0);
```

จะเห็นว่ามันเหมือนกันเลยครับ ดังนั้นควรใช้ mix ดีกว่าเนอะ

นอกจากนี้ยังมีความยืดหยุ่นกว่า เพราะสามารถใส่ค่าสีได้โดยตรง เช่นถ้าอยากทำ gradient แบบ [uigradients.com/#VelvetSun](https://uigradients.com/#VelvetSun) ก็แค่เอาค่าสีมาใส่ในตัวแปรแบบนี้ได้เลย

`shader-gradient.frag`

```c
varying vec2 juju_texCoord;

void main() {
    float value = juju_texCoord.s;
    const vec4 color1 = vec4(0.882, 0.933, 0.765, 1.0);
    const vec4 color2 = vec4(0.941, 0.314, 0.325, 1.0);
    gl_FragColor = mix(color1, color2, value);
}
```

ผลลัพธ์ที่ได้ก็เป็น gradient สวยๆที่เราคาดหวังไว้ :D

![](https://telluric-chimneys.000webhostapp.com/images/shader-21.png)

ลองเขียน gradient ในรูปแบบอื่นๆเล่นดูครับ อย่างเช่น แบบแบ่งครึ่งบนล่าง แบบนี้

`shader-gradient.frag`

```c
varying vec2 juju_texCoord;

void main() {
    float value = juju_texCoord.s;
    const vec4 color1 = vec4(0.882, 0.933, 0.765, 1.0);
    const vec4 color2 = vec4(0.941, 0.314, 0.325, 1.0);
    const vec4 color3 = vec4(0.0, 0.765, 1.0, 1.0);
    const vec4 color4 = vec4(1.0, 1.0, 0.11, 1.0);

    if (juju_texCoord.t <= 0.5) {
        gl_FragColor = mix(color1, color2, value);
    } else {
        gl_FragColor = mix(color3, color4, value);
    }
}
```

จะเห็นว่าผมใช้ `if else` เช็กตำแหน่งว่าอยู่ครึ่งบนหรือครึ่งล่างจากการอ่านค่า `juju_texCoord.t` และนี่คือผลลัพธ์ที่ได้

![](https://telluric-chimneys.000webhostapp.com/images/shader-22.png)

หรือจะลองเขียน gradient 3 สีของ instagram แบบแนวตั้ง แบบนี้

```c
varying vec2 juju_texCoord;

void main() {
    float value = juju_texCoord.t;
    const vec4 color1 = vec4(0.514, 0.227, 0.706, 1.0);
    const vec4 color2 = vec4(0.992, 0.114, 0.114, 1.0);
    const vec4 color3 = vec4(0.988, 0.69, 0.271, 1.0);

    if (juju_texCoord.t <= 0.5) {
        gl_FragColor = mix(color1, color2, value * 2.0);
    } else {
        gl_FragColor = mix(color2, color3, (value - 0.5) * 2.0);
    }
}
```

ผลลัพธ์ที่ได้

![](https://telluric-chimneys.000webhostapp.com/images/shader-23.png)

ก็ลองไปหัดเขียนเล่นๆกันดูนะครับ อย่างน้อยต้องเข้าใจค่า `s` และ `t` ของระบบพิกัด texture และเข้าใจ output ที่ได้จาก function **mix** สักหน่อย

สำหรับในตอนนี้ขอพักไว้เท่านี้ครับ เดี๋ยวจะเหนื่อยกันเกินไป พบกันใหม่ในตอนถัดไปครับ รับรองว่ามีประโยชน์มากขึ้นเรื่อยๆ รอติดตามกันได้เลย ^^