---
layout: post
author: skyousuke
title: "Shader ใน LibGDX เบื้องต้น ตอนที่ 4"
category: "Shader"
image: https://telluric-chimneys.000webhostapp.com/images/shader-32.jpg
lang: th_TH
---

เราเดินทางกันมาจนถึงตอนที่ 4 แล้ว ในตอนนี้เราจะเริ่มมาเขียน shader ที่เอาไว้ใช้งานจริงๆกันดีกว่าครับ

## Shader เปลี่ยนภาพเป็นโทนขาวดำ

shader ตัวแรกที่เราจะเขียนกันก็คือ shader ที่แปลงสีให้เป็นโทนขาวดำ ก่อนอื่นเรามาทำความรู้จักกับคำว่า **ขาวดำ** กันก่อนน่าจะดี

**ขาวดำ** ก็คือค่าสี RGB ที่เท่ากันทั้ง 3 ตัว ถ้ามีค่าน้อยจะเข้าใกล้สีดำ ถ้ามีค่ามากจะเข้าใกล้สีขาว ดังรูป

![](https://telluric-chimneys.000webhostapp.com/images/shader-33.png)

ดังนั้น ภาพที่เป็นโทนขาวดำ คือภาพที่**แต่ละ pixel มีค่า R=G=B** นั่นเอง โดยค่าที่ว่านี้จะแตกต่างกันในแต่ละ pixel ทำให้มีความเข้มอ่อนต่างกันจนเป็นรูปภาพขาวดำขึ้นมานั่นแหละ แล้วค่าความเข้มอ่อนกำหนดจากอะไรล่ะ?

ในการกำหนดค่าความเข้มอ่อน มีหลายวิธีมากๆ (เกิน 10 วิธี) แต่ในบทความนี้ ผมจะเขียนให้ดู 3 วิธี 

### 1. ใช้วิธีเฉลี่ยค่า RGB 

หลักการก็คือ เอาค่าสี RGB ทั้ง 3 มาเฉลี่ยกันเลย ดังนั้น จะได้ว่า

```
ค่าความเข้ม = (R + G + B) / 3
```

มาลงมือเขียน shader กันเลยครับ ก่อนอื่น vertex shader ตั้งชื่อว่า `shader-gray-1.vert` และให้เราเขียนส่งค่าพิกัด texture ไปยัง fragment shader เหมือนเดิมที่เคยเขียนไปแล้ว ดังนี้

`shader-gray-1.vert`

```c
uniform mat4 u_projTrans;

attribute vec4 a_position;
attribute vec2 a_texCoord0;

varying vec2 juju_texCoord;

void main() {
    gl_Position = u_projTrans * a_position;
    juju_texCoord = a_texCoord0;
}
```

**หมายเหตุ:** หากไม่เข้าใจความหมายแต่ละบรรทัด ให้กลับไปอ่านตอนที่ 1 และ 2 อีกครั้ง มีอธิบายไว้ละเอียดแล้ว
{: .notice}

ส่วน fragment shader เราจะเริ่มต้นง่ายๆ โดยการอ่านค่าสีจาก texture ก่อน

`shader-gray-1.frag`

```c
uniform sampler2D u_texture;

varying vec2 juju_texCoord;

void main() {
    vec4 texColor = texture2D(u_texture, juju_texCoord);
} 
```

เมื่อได้ค่าสีมาแล้วก็นำมาหาค่าความเข้มเฉลี่ย

`shader-gray-1.frag`

```c
uniform sampler2D u_texture;

varying vec2 juju_texCoord;

void main() {
    vec4 texColor = texture2D(u_texture, juju_texCoord);
    float average = (texColor.r + texColor.g + texColor.b) / 3.0;
} 
```

เอาค่าเฉลี่ยที่ได้มาเป็นค่าสี RGB

`shader-gray-1.frag`

```c
uniform sampler2D u_texture;

varying vec2 juju_texCoord;

void main() {
    vec4 texColor = texture2D(u_texture, juju_texCoord);
    float average = (texColor.r + texColor.g + texColor.b) / 3.0;
    gl_FragColor = vec4(average, average, average, 1.0);
} 
```

เพียงเท่านี้ก็พร้อมใช้งาน ลองเอาไป run ดูได้ผลลัพธ์ดังรูป

![](https://telluric-chimneys.000webhostapp.com/images/shader-34.png)

### 2. ใช้วิธีแสงเฉลี่ย

หลักการก็คือ เอาค่าสีที่เด่นสุด (ความเข้มสูงสุด) เฉลี่ยกับค่าสีที่เด่นน้อยสุด (ความเข้มน้อยสุด) ดังนั้น จะได้ว่า

```
ค่าความเข้ม = (max(R, G, B) + min(R, G, B)) / 2
```

มาเขียน shader ต่อกันเลย เริ่มจาก vertex shader ให้สร้างไฟล์ใหม่ชื่อ `shader-gray-2.vert` แล้วก็เขียนเหมือนเดิมนั่นแหละ (copy มาจาก `shader-gray-1.vert` ได้เลย)

`shader-gray-2.vert` 

```c
uniform mat4 u_projTrans;

attribute vec4 a_position;
attribute vec2 a_texCoord0;

varying vec2 juju_texCoord;

void main() {
    gl_Position = u_projTrans * a_position;
    juju_texCoord = a_texCoord0;
}
```

ส่วน fragment shader ให้สร้างไฟล์ใหม่ชื่อ `shader-gray-2.frag` และเริ่มเขียนเหมือนเดิมก็คืออ่านค่าสี texture ก่อน

`shader-gray-2.frag`

```c
uniform sampler2D u_texture;

varying vec2 juju_texCoord;

void main() {
    vec4 texColor = texture2D(u_texture, juju_texCoord);
} 
```

ทีนี้ **การหาค่าสูงสุด หรือต่ำสุด** ให้ใช้ built-in function ที่มากับ glsl ได้เลยมี 2 function ด้วยกันก็คือ

```c
float max(float x, float y);
```

**max** จะให้ค่าสูงสุดจาก 2 ค่าที่ใส่เข้าไป (ให้ค่าตัวที่มากกว่านั่นแหละ)

```c
float min(float x, float y);
```

**min** จะให้ค่าต่ำสุดจาก 2 ค่าที่ใส่เข้าไป (ให้ค่าตัวที่น้อยกว่านั่นแหละ)

ดังนั้นเราสามารถเขียนหาความเข้มจากค่าแสงเฉลี่ยได้แบบนี้

`shader-gray-2.frag`

```c
uniform sampler2D u_texture;

varying vec2 juju_texCoord;

void main() {
    vec4 texColor = texture2D(u_texture, juju_texCoord);
    float max = max(max(texColor.r, texColor.g), texColor.b);
    float min = min(min(texColor.r, texColor.g), texColor.b);
    float lightness = (max + min) / 2;
} 
```

เอาค่าแสงเฉลี่ยที่ได้มาเป็นค่าสี RGB เลย

`shader-gray-2.frag`

```c
uniform sampler2D u_texture;

varying vec2 juju_texCoord;

void main() {
    vec4 texColor = texture2D(u_texture, juju_texCoord);
    float max = max(max(texColor.r, texColor.g), texColor.b);
    float min = min(min(texColor.r, texColor.g), texColor.b);
    float lightness = (max + min) / 2;
    gl_FragColor = vec4(lightness, lightness, lightness, 1.0);
} 
```

เสร็จแล้ว ลองเอาไป run ดูได้ผลลัพธ์ดังรูป

![](https://telluric-chimneys.000webhostapp.com/images/shader-35.png)

### 3. ใช้วิธีความสว่าง

หลักการคล้ายกับวิธีเฉลี่ยค่าสี RGB แต่ว่าจะมีการใช้การรับรู้สีของมนุษย์มาร่วมด้วย โดยพวกเราจะรับรู้แสงสีเขียวได้มากกว่าสีอื่น รองลงมาคือสีแดง ส่วนสีน้ำเงินรับรู้ได้น้อยสุด ดังนั้น ในการเฉลี่ยเราจะให้ค่าน้ำหนักไปที่เขียวมากที่สุด และให้ค่าน้ำหนักสีน้ำเงินน้อยสุด ตามสูตรนี้

```
ค่าความเข้ม = R * 0.299 + G * 0.587 + B *0.114
```

**ข้อสังเกต:** 0.299 + 0.587 + 0.114 = 1.0
{: .notice}

ทีนี้ก็เขียน shader เพื่อดูผลลัพธ์เหมือนเดิม เริ่มจาก vertex shader ให้สร้างไฟล์ใหม่ชื่อ `shader-gray-3.vert` แล้วก็เขียนเหมือนเดิมอีกนั่นแหละ - -*

`shader-gray-3.vert` 

```c
uniform mat4 u_projTrans;

attribute vec4 a_position;
attribute vec2 a_texCoord0;

varying vec2 juju_texCoord;

void main() {
    gl_Position = u_projTrans * a_position;
    juju_texCoord = a_texCoord0;
}
```

ส่วน fragment shader ให้สร้างไฟล์ใหม่ชื่อ `shader-gray-3.frag` และเริ่มเหมือนเดิม

`shader-gray-3.frag`

```c
uniform sampler2D u_texture;

varying vec2 juju_texCoord;

void main() {
    vec4 texColor = texture2D(u_texture, juju_texCoord);
} 
```

หาค่าความสว่างจากสูตรการรับรู้สี และกำหนดค่าสี RGB ตามค่าที่คำนวณได้

`shader-gray-3.frag`

```c
uniform sampler2D u_texture;

varying vec2 juju_texCoord;

void main() {
    vec4 texColor = texture2D(u_texture, juju_texCoord);
    float luminosity = texColor.r * 0.299 + texColor.g * 0.587 + texColor.b * 0.114;
    gl_FragColor = vec4(luminosity, luminosity, luminosity, 1.0);
} 
```

เมื่อลอง run จะได้ผลลัพธ์ดังรูป

![](https://telluric-chimneys.000webhostapp.com/images/shader-36.png)


### เปรียบเปรียบ 3 แบบ

ผลลัพธ์ค่อนข้างคล้ายกันมาก แต่มีจุดแตกต่างที่พอสังเกตได้อยู่ (เช่น ลายจุดบนชุดนอนมิซึฮะ) 

![](https://telluric-chimneys.000webhostapp.com/images/shader-37.png)

ดังนั้นจะใช้วิธีไหนก็ได้ แต่แนะนำแบบวิธีความสว่าง เพราะจะให้ภาพโทนขาวดำที่ใกล้เคียงกับภาพต้นฉบับที่เป็นสีมากที่สุด


## Shader เปลี่ยนภาพเป็นแนว Sepia

การทำภาพเป็นแนว sepia (ภาพโทนสีน้ำตาลอ่อน ให้ความรู้สึกเป็นภาพเก่าๆ) นั้น สามารถทำได้หลายวิธี แต่เราจะแนะนำวิธีง่ายๆที่สุดอยู่ 2 วิธีดังนี้

### 1. แปลงเป็นโทนขาวดำ แล้วคูณด้วยค่าสี Sepia

วิธีนี้เราจะเอา shader แปลงภาพขาวดำอันที่แล้วมาต่อยอด ก่อนอื่นเขียน vertex shader เหมือนเดิม 

`shader-sepia-1.vert` 

```c
uniform mat4 u_projTrans;

attribute vec4 a_position;
attribute vec2 a_texCoord0;

varying vec2 juju_texCoord;

void main() {
    gl_Position = u_projTrans * a_position;
    juju_texCoord = a_texCoord0;
}
```

ส่วน frament shader นั้นให้ copy จาก shader ที่แปลงภาพขาวดำด้วยวิธีความสว่าง

`shader-sepia-1.frag`

```c
uniform sampler2D u_texture;

varying vec2 juju_texCoord;

void main() {
    vec4 texColor = texture2D(u_texture, juju_texCoord);
    float luminosity = texColor.r * 0.299 + texColor.g * 0.587 + texColor.b * 0.114;
    gl_FragColor = vec4(luminosity, luminosity, luminosity, 1.0);
} 
```

สร้างตัวแปร `grayscale` เพื่อเก็บค่าสี rbg แบบโทนขาวดำ และสร้างตัวแปร `sepia` และกำหนดค่าให้เท่ากับ `vec3(1.2, 1.0, 0.8)` (ค่าสี sepia) ดังนี้

`shader-sepia-1.frag`

```c
uniform sampler2D u_texture;

varying vec2 juju_texCoord;

void main() {
    vec4 texColor = texture2D(u_texture, juju_texCoord);
    float luminosity = texColor.r * 0.299 + texColor.g * 0.587 + texColor.b * 0.114;
    vec3 grayscale = vec3(luminosity, luminosity, luminosity);    
    vec3 sepia = vec3(1.2, 1.0, 0.8);

    gl_FragColor = vec4(luminosity, luminosity, luminosity, 1.0);
} 
```

นำ `grayscale` มาคูณกับ `sepia` เพื่อได้ค่าสีสุดท้าย และนำไปใส่ใน `gl_FragColor` เป็นอันเสร็จ

`shader-sepia-1.frag`

```c
uniform sampler2D u_texture;

varying vec2 juju_texCoord;

void main() {
    vec4 texColor = texture2D(u_texture, juju_texCoord);
    float luminosity = texColor.r * 0.299 + texColor.g * 0.587 + texColor.b * 0.114;
    vec3 grayscale = vec3(luminosity, luminosity, luminosity);    
    vec3 sepia = vec3(1.2, 1.0, 0.8);
    vec3 finalColor = grayscale * sepia;
    gl_FragColor = vec4(finalColor.r, finalColor.g, finalColor.b, 1.0);
} 
```

เรามาลอง run แล้วดูผลลัพธ์กันดู

![](https://telluric-chimneys.000webhostapp.com/images/shader-38.png)

ภาพนี้เห็น effect ไม่ชัด ลองดูภาพนี้ดีกว่า

![](https://telluric-chimneys.000webhostapp.com/images/shader-32.jpg)

จบไปสำหรับวิธีที่ 1 ผลลัพธ์ก็โอเค และมาดูอีกวิธีกัน

### 2. แปลงโดยใช้สูตร Sepia โดยตรง

วิธีนี้จะแตกต่างจากวิธีที่ 1 ตรงที่เราไม่ต้องแปลงเป็นขาวดำก่อน แต่คูณ RGB ด้วยสูตรโดยตรง ซึ่งสูตรการแปลงค่าสี RGB เป็นสี sepia มีสูตรดังนี้

```
R = (R * .393) + (G *.769) + (B * .189)
G = (R * .349) + (G *.686) + (B * .168)
B = (R * .272) + (G *.534) + (B * .131)
หากค่าสีใดมีผลลัพธ์เกิน 1.0 จะปัดลงให้เหลือ 1.0
```

เราก็เอาสูตรมาสร้าง shader กันเลย ในส่วนของ vertex shader เราเขียนเหมือนเดิมทุกอย่าง

`shader-sepia-2.vert` 

```c
uniform mat4 u_projTrans;

attribute vec4 a_position;
attribute vec2 a_texCoord0;

varying vec2 juju_texCoord;

void main() {
    gl_Position = u_projTrans * a_position;
    juju_texCoord = a_texCoord0;
}
```

ส่วนของ fragment shader เราก็เอาสูตรมาใช้เลย

`shader-sepia-2.frag`

```c
uniform sampler2D u_texture;

varying vec2 juju_texCoord;

void main() {
    vec4 texColor = texture2D(u_texture, juju_texCoord);
    float r = texColor.r * 0.393 + texColor.g * 0.769 + texColor.b * 0.189;
    float g = texColor.r * 0.349 + texColor.g * 0.686 + texColor.b * 0.168;
    float b = texColor.r * 0.272 + texColor.g * 0.534 + texColor.b * 0.131;
    gl_FragColor = vec4(r, g, b, 1.0);
} 
```

แต่อย่าลืมว่า ผลลัพธ์ของค่า r หรือ g หรือ b อาจจะเกิน 1.0 ได้ ดังนั้นเราต้อง limit มันด้วย function **min** แบบนี้

`shader-sepia-2.frag`

```c
uniform sampler2D u_texture;

varying vec2 juju_texCoord;

void main() {
    vec4 texColor = texture2D(u_texture, juju_texCoord);
    float r = texColor.r * 0.393 + texColor.g * 0.769 + texColor.b * 0.189;
    float g = texColor.r * 0.349 + texColor.g * 0.686 + texColor.b * 0.168;
    float b = texColor.r * 0.272 + texColor.g * 0.534 + texColor.b * 0.131;
    gl_FragColor = vec4(min(r, 1.0), min(g, 1.0), min(b, 1.0), 1.0);
} 
```

**ข้อสังเกต:** min(x, 1.0)  จะให้ค่าเป็น x ถ้า x น้อยกว่า 1.0 แต่ถ้า x มากกว่าหรือเท่ากับ 1.0 จะให้ค่าเป็น 1.0 หรือก็คือ เป็นการจำกัดค่าไม่ให้เกิน 1.0 นั่นเอง
{: .notice}

เรามาลอง run แล้วดูผลลัพธ์กันดู

![](https://telluric-chimneys.000webhostapp.com/images/shader-39.jpg)

สังเกตว่าแบบที่ 2 จะให้ภาพที่สว่างกว่าแบบที่ 1 พอสมควรเลย เลือกอันที่ชอบเนอะ สำหรับการเขียน shader ทำภาพ sepia ก็มีเพียงเท่านี้

## Shader เปลี่ยนภาพเป็นสีตรงข้าม (inverted colors)

มาทำภาพที่เป็นสีตรงข้ามกันแบบง่ายๆด้วย shader กันดีกว่า หลักการก็คือเอา 1.0 ที่เป็นค่าสีสูงสุดมาลบกับ RGB (เพื่อให้เปลี่ยนเป็นสีตรงข้าม) แบบนี้

```
R = 1.0 - R
G = 1.0 - G
B = 1.0 - B
```

ก่อนอื่นเริ่มจาก vertex shader ที่เหมือนเดิมทุกครั้ง

`shader-invert.vert` 

```c
uniform mat4 u_projTrans;

attribute vec4 a_position;
attribute vec2 a_texCoord0;

varying vec2 juju_texCoord;

void main() {
    gl_Position = u_projTrans * a_position;
    juju_texCoord = a_texCoord0;
}
```

fragment shader ก็เอามาสูตรมาใส่ได้เลย

`shader-invert.frag`

```c
uniform sampler2D u_texture;

varying vec2 juju_texCoord;

void main() {
    vec4 texColor = texture2D(u_texture, juju_texCoord);
    float r = 1.0 - texColor.r;
    float g = 1.0 - texColor.g;
    float b = 1.0 - texColor.b;
    gl_FragColor = vec4(r, g, b, 1.0);
} 
```

เสร็จละ ลองเอาไป run ดูกันเถอะ

![](https://telluric-chimneys.000webhostapp.com/images/shader-40.jpg)

สำหรับ shader เปลี่ยนสีตรงข้าม ก็มีเพียงเท่านี้แหละจ่ะ (สั้นไปเปล่านะ 555+)

## เปลี่ยนภาพให้เป็นโทนสีเดียวกัน

บางทีเราอยากได้ภาพที่เป็นโทนสีใดสีหนึ่งไปเลย ก็มีหลักการทำง่ายๆอยู่ 2 วิธี ดังนี้

### 1. แปลงภาพเป็นโทนสีขาวดำ แล้วนำไปคูณด้วยค่าสี

เขียน vertex shader เหมือนเดิม (อีกแล้ว!)

`shader-color-1.vert` 

```c
uniform mat4 u_projTrans;

attribute vec4 a_position;
attribute vec2 a_texCoord0;

varying vec2 juju_texCoord;

void main() {
    gl_Position = u_projTrans * a_position;
    juju_texCoord = a_texCoord0;
}
```

ส่วน fragment shader ให้เขียนแปลงเป็นขาวดำก่อน

`shader-color-1.frag`

```c
uniform sampler2D u_texture;

varying vec2 juju_texCoord;

void main() {
    vec4 texColor = texture2D(u_texture, juju_texCoord);
    float luminosity = texColor.r * 0.299 + texColor.g * 0.587 + texColor.b * 0.114;
    vec3 grayscale = vec3(luminosity, luminosity, luminosity);    
} 
```

จากนั้นนำไปคูณกับค่าสีที่ต้องการ เช่นในกรณีนี้เป็นสีแดง `vec3 red = vec(1.0, 0.0, 0.0)`

`shader-color-1.frag`

```c
uniform sampler2D u_texture;

varying vec2 juju_texCoord;

void main() {
    vec4 texColor = texture2D(u_texture, juju_texCoord);
    float luminosity = texColor.r * 0.299 + texColor.g * 0.587 + texColor.b * 0.114;
    vec3 grayscale = vec3(luminosity, luminosity, luminosity);  
    vec3 red = vec3(1.0, 0.0, 0.0);
    vec3 finalColor = grayscale * red;
    gl_FragColor = vec4(finalColor.r, finalColor.g, finalColor.b, 1.0);
} 
```

ลอง run จะได้ผลลัพธ์แบบนี้

![](https://telluric-chimneys.000webhostapp.com/images/shader-41.jpg)

แดงแจ๋เลยแหะ ลองมาดูแบบที่ 2 กันบ้าง


### 2. นำสีเดิมไปคูณด้วยค่าสีโดยตรง

หลักการก็ใช้การคูณปกตินั่นแหละ ถ้ากำหนดให้สีเดิมคือ `RBG` และสีที่เราต้องการคือ `rgb` จะได้ว่า

```
R = R * r;
G = G * g;
B = B * b;
```

เริ่มจาก vertex shader เหมือนเดิม (เบื่อแล้วที่ต้องพูดซ้ำๆ 55)

`shader-color-2.vert` 

```c
uniform mat4 u_projTrans;

attribute vec4 a_position;
attribute vec2 a_texCoord0;

varying vec2 juju_texCoord;

void main() {
    gl_Position = u_projTrans * a_position;
    juju_texCoord = a_texCoord0;
}
```

ส่วน fragment shader เราก็เอาค่าสีที่ต้องการมาคูณแบบได้เลย แบบนี้

`shader-color-2.frag`

```c
uniform sampler2D u_texture;

varying vec2 juju_texCoord;

void main() {
    vec4 texColor = texture2D(u_texture, juju_texCoord);
    vec3 red = vec3(1.0, 0.0, 0.0);
    float r = texColor.r * red.r;
    float g = texColor.g * red.g;
    float b = texColor.b * red.b;
    gl_FragColor = vec4(r, g, b, 1.0);
}
```

มาลอง run กันดู

![](https://telluric-chimneys.000webhostapp.com/images/shader-42.jpg)

จะเห็นว่าได้ภาพสีแดงเหมือนกัน แต่ว่า**แสงมีความเพี้ยนจากต้นฉบับ**ไปข้างค่อนเยอะ ส่วนที่ควรสว่างก็มืดลงไป บางส่วนก็สว่างมากเกินไป เนื่องจากเราไม่ได้ได้มีการปรับค่าน้ำหนักของสีก่อน ทำให้บาง component มีค่าต่ำหรือสูงกว่า ค่าควรที่จะเป็นนั่นเอง ดังนั้นควรเลือกใช้แบบที่ 1 เนอะ

สำหรับ shader การแต่งสีก็มีเพียงเท่านี้ ลองไปใช้สีอื่นที่ไม่ใช่สีแดงดูกันนะ!

## ทำให้ Code อ่านง่ายขึ้น

เราสามารถทำให้ code ของเราที่ยาวๆ สั้นลงและอ่านง่ายขึ้นได้ โดยใช้การเขียนย่อๆได้ ดังต่อไปนี้

### 1. สร้าง vector ย่อยจาก vector ที่ใหญ่กว่า

เราสร้าง `vec2` จาก `vec3` หรือ `vec4` ได้แบบนี้

```c
vec4 someColor1 = vec4(1.0, 0.7, 0.5, 0.9);
vec3 someColor2 = vec3(1.0, 0.7, 0.5);

vec2 redGreen = vec2(someColor1.r, someColor1.g);
vec2 redBlue = vec2(someColor2.r, someColor2.g);
vec2 greenBlue = vec2(someColor1.g, someColor1.b);
vec2 greenAlpha = vec2(someColor1.g, someColor1.a);
```

เขียนใหม่ได้เป็น

```c
vec4 someColor1 = vec4(1.0, 0.7, 0.5, 0.9);
vec3 someColor2 = vec3(1.0, 0.7, 0.5);

vec2 redGreen = someColor1.rg;
vec2 redBlue = someColor2.rb;
vec2 greenBlue = someColor1.gb;
vec2 greenAlpha = someColor1.ga;
```

เราสร้าง `vec3` จาก `vec4` ได้เช่นกัน

```c
vec4 someColor = vec4(1.0, 0.7, 0.5, 0.9);

vec3 rgbColor = vec3(someColor.r, someColor.g, someColor.b);
vec3 rgaColor = vec3(someColor.r, someColor.g, someColor.a);
```

เขียนใหม่ได้เป็น

```c
vec4 someColor = vec4(1.0, 0.7, 0.5, 0.9);

vec3 rgbColor = someColor.rgb;
vec3 rgaColor = someColor.rga;
```

### 2. บวกลบคูณหารค่าภายใน vector พร้อมกัน

เราเอาค่าคงที่มาบวกลบคูณหารกับค่าภายใน vector ทุกตัวพร้อมๆกันได้เลย

```c
vec4 color;

vec4(color.r - 0.1, color.g - 0.1, color.b - 0.1, color.a - 0.1);
vec4(1.0 - color.r, 1.0 - color.g, 1.0 - color.b, 1.0 - color.a);
vec4(0.5 * color.r, 0.5 * color.g, 0.5 * color.b, 0.5 * color.a);
vec4(color.r / 2.0, color.g / 2.0, color.b / 2.0, color.a / 2.0);
```

เขียนใหม่ได้เป็น

```c
vec4 color;

color - 0.1;
1.0 - color;
0.5 * color;
color / 2.0;
```

### 3. กำหนดค่า vector ใหญ่ด้วย vector ย่อย

เรากำหนดค่า `vec4` ด้วย `vec3` หรือ `vec2` แบบนี้ได้

```c
vec3 someColor;
gl_FragColor = vec4(someColor.r, someColor.g, someColor.b, 1.0);

vec2 someRedGreen;
gl_FragColor = vec4(someRedGreen.r, someRedGreen.g, 0.7, 1.0);
```

สามาถเขียนใหม่ได้เป็น

```c
vec3 someColor;
gl_FragColor = vec4(someColor, 1.0);

vec2 someRedGreen;
gl_FragColor = vec4(someRedGreen, 0.7, 1.0);
```

หรือกำหนดค่า `vec3` ด้วย `vec2` ก็ได้

```c
vec3 someColor;
vec2 someRedGreen;

someColor = vec4(someRedGreen.r, someRedGreen.g, 0.5);
```

สามาถเขียนใหม่ได้เป็น

```c
vec3 someColor;
vec2 someRedGreen;

someColor = vec4(someRedGreen, 0.5);
```

### 4. จับ vector คูณกับ vector

ถ้าเรามีเลขที่ต้องจับคูณกันหลายคู่ ให้เอา vector คูณกันจะง่ายกว่า เช่น

```c
vec3 color1;
vec3 color2;
vec3 finalColor;

finalColor.r = color1.r * color2.r;
finalColor.g = color1.g * color2.g;
finalColor.b = color1.b * color2.b;
```

สามาถเขียนใหม่ได้เป็น

```c
vec3 someColor;
vec2 someRedGreen;
vec3 finalColor = color1 * color2;
```


### น้ำวิธีทั้ง 4 มาใช้

จากวิธีการทั้ง 4 เราลองมาปรับปรุง code เดิมให้อ่านง่ายกันดีกว่า 

**ตัวอย่างที่ 1**

`shader-color-2.frag`

```c
uniform sampler2D u_texture;

varying vec2 juju_texCoord;

void main() {
    vec4 texColor = texture2D(u_texture, juju_texCoord);
    vec3 red = vec3(1.0, 0.0, 0.0);
    float r = texColor.r * red.r;
    float g = texColor.g * red.g;
    float b = texColor.b * red.b;
    gl_FragColor = vec4(r, g, b, 1.0);
}
```

จาก code นี้มีการคูณกัน 3 คู่ และเราสามารถสร้างสี RGB จาก `texColor` โดยการเขียนเป็น `texColor.rgb` ได้ ดังนั้นเราเขียนใหม่ได้ดังนี้

`shader-color-2.frag`

```c
uniform sampler2D u_texture;

varying vec2 juju_texCoord;

void main() {
    vec4 texColor = texture2D(u_texture, juju_texCoord);
    vec3 red = vec3(1.0, 0.0, 0.0);
    vec3 finalColor = texColor.rgb * red;
    gl_FragColor = vec4(finalColor.r, finalColor.g, finalColor.b, 1.0);
}
```

เราสามารถกำหนดค่า `vec4` ด้วย `vec3` ได้ ดังนั้นจึงเขียนใหม่ได้เป็น

```c
uniform sampler2D u_texture;

varying vec2 juju_texCoord;

void main() {
    vec4 texColor = texture2D(u_texture, juju_texCoord);
    vec3 red = vec3(1.0, 0.0, 0.0);
    gl_FragColor = vec4(texColor.rgb * red, 1.0);
}
```

สังเกตว่าสั้นลงและอ่านง่ายขึ้น เราลองมาดูตัวอย่างอื่นบ้าง

**ตัวอย่างที่ 2**

`shader-color-1.frag`

```c
uniform sampler2D u_texture;

varying vec2 juju_texCoord;

void main() {
    vec4 texColor = texture2D(u_texture, juju_texCoord);
    float luminosity = texColor.r * 0.299 + texColor.g * 0.587 + texColor.b * 0.114;
    vec3 grayscale = vec3(luminosity, luminosity, luminosity);  
    vec3 red = vec3(1.0, 0.0, 0.0);
    vec3 finalColor = grayscale * red;
    gl_FragColor = vec4(finalColor.r, finalColor.g, finalColor.b, 1.0);
} 
```

เอา `finalColor` ออกไป และนำ ` grayscale * red` ไปใส่ใน `vec4` ได้เลย 

`shader-color-1.frag`

```c
uniform sampler2D u_texture;

varying vec2 juju_texCoord;

void main() {
    vec4 texColor = texture2D(u_texture, juju_texCoord);
    float luminosity = texColor.r * 0.299 + texColor.g * 0.587 + texColor.b * 0.114;
    vec3 grayscale = vec3(luminosity, luminosity, luminosity);  
    vec3 red = vec3(1.0, 0.0, 0.0);
    gl_FragColor = vec4(grayscale * red, 1.0);
} 
```

**ตัวอย่างที่ 3**

`shader-invert.frag`

```c
uniform sampler2D u_texture;

varying vec2 juju_texCoord;

void main() {
    vec4 texColor = texture2D(u_texture, juju_texCoord);
    float r = 1.0 - texColor.r;
    float g = 1.0 - texColor.g;
    float b = 1.0 - texColor.b;
    gl_FragColor = vec4(r, g, b, 1.0);
} 
```

จาก code จะเห็นว่าค่า rgb เท่ากับ 1.0 - rgb ดังนั้นเขียนใหม่เป็น

`shader-invert.frag`

```c
uniform sampler2D u_texture;

varying vec2 juju_texCoord;

void main() {
    vec4 texColor = texture2D(u_texture, juju_texCoord);
    gl_FragColor = vec4(1.0 - texColor.rgb, 1.0);
} 
```

3 ตัวอย่างน่าจะเพียงพอแล้วเนอะ ลองไปหัดปรับปรุงให้ code อ่านง่ายกันด้วยนะครับ 

สำหรับในตอนที่ 4 มีเพียงเท่านี้ เราได้เขียน effect สีไปทั้งหมด 4 แบบ

1. โทนขาวดำ
2. โทน sepia
3. โทนสีตรงข้าม
4. โทนสีที่ต้องการ

ในตอนถัดไปเราจะมาดูวิธีการส่งค่าตัวแปรจาก java code (โดยใช้ libGDX) มายัง shader เพื่อสร้าง shader ในระดับที่สูงขึ้นกันครับ สำหรับตอนนี้ขอตัวก่อน ไปฝึกกันให้คล่องๆเนอะ บ๊าย บาย ^^